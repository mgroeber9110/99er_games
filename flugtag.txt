100 !
110 !*********************
120 !*      FLUGTAG      *
130 !*(C) BY MG-SOFT 1984*
140 !*********************
150 !
160 CALL CLEAR :: CALL MAGNIFY(3)
170 RANDOMIZE :: DIM WI(9)
180 CALL SCREEN(5) :: FOR A=0 TO 8 :: CALL COLOR(A,2,8) :: NEXT A
190 CALL COLOR(11,9,8,12,13,8,13,2,15)
200 FOR A=65 TO 90 :: CALL CHARPAT(A+32,A$) :: CALL CHAR(A,"FF"&SEG$(A$,5,12)&"FF") :: NEXT A
210 CALL CHAR(60,"01030509112141FF",62,RPT$("80",8))
220 CALL CHAR(96,"00030F1F3F383D3D3F1F0F070302010100C0F0F8FCFCBCBCBCF8F0E0C0408080")
230 CALL CHAR(64,"000000EC92929292")
240 FLGL$="000000000060E060202070FBACDFDF"&RPT$("0",24)&"FF03FFFE" :: CALL CHAR(100,FLGL$)
250 FLGR$="0000000000303830202070FBACDFDF"&RPT$("0",24)&"FF03FFFE"
260 CALL CHAR(108,"00000080DFFFF0F5F5F7FFCF8000000000000000F8FC465F5746FCF8E0E0")
270 CALL CHAR(104,RPT$("0",20)&"2A1C0A091401"&RPT$("0",20)&"150EA4CA8040")
280 CH$="0102040810204080"
290 FOR A=1 TO 8 :: CALL CHAR(111+A,RPT$(SEG$(CH$,A*2-1,2),8)) :: NEXT A
300 CALL CHAR(91,"80F0F8DBFB737FFF"&RPT$("0",14)&"FF010F1FDBDFCECEFF00003C183C3C3C7E")
310 CALL CHAR(120,RPT$("0",16)&"003C7E7E3C18181800183C3C7E3C7E18183C7E7E7E3C1818")
320 CALL CHAR(124,"000000000F95A4FFBF9F0C1C08000000000000000183FFFFFFC702")
330 CALL CHAR(128,"0000013DFFFFFFFF7F7F2100000000000000C0E0F8FCFEFEFEFCF8")
340 CALL CHAR(132,"60FF0000000000F0F00000000060FF")
350 CALL CHAR(32,RPT$("00",15)&"01"&RPT$("00",16))
360 CALL CHAR(36,"0000000000000000000103050F3F3F0C000000000000000040F8FCBC1CBCFC18")
370 CALL CHAR(136,"00924A0000EEA8EC888E00004A920000004952000097D4B5959700005249")
380 CALL CHAR(140,RPT$("00",9)&"070E0805030102"&RPT$("00",9)&"C0602040800080") :: GOSUB 1230
390 CALL HCHAR(1,1,120,608) :: CALL VCHAR(20,3,91) :: CALL VCHAR(20,31,93) :: FOR A=0 TO 8 :: CALL COLOR(A,2,4) :: NEXT A
400 CALL SPRITE(#16,128,16,3,100,0,4,#17,128,16,8,30,0,5,#18,128,16,22,190,0,6,#19,128,16,3,2,0,7)
410 CALL HCHAR(22,1,132,32) :: CALL HCHAR(23,1,133,32)
420 X=3 :: C=119 :: FOR Y=19 TO 7 STEP-2 :: CALL VCHAR(Y-1,X,C,2) :: CALL VCHAR(Y-1,34-X,231-C,2) :: C=C-1 :: IF C=111 THEN C=119 :: X=X+1
430 NEXT Y :: CALL SPRITE(#3,108,7,30,14,#4,108,7,30,233)
440 CALL SPRITE(#1,124,7,168,235)
450 FOR X=4 TO 30 :: IF RND<.5 THEN CALL VCHAR(19,X,121+INT(RND*3))
460 NEXT X :: CALL MOTION(#1,0,-10)
470 FOR A=30 TO 14 STEP-1 :: CALL SOUND(-999,-3,A) :: NEXT A
480 CALL SOUND(-999,-3,14) :: CALL POSITION(#1,Y,X) :: IF X>120 THEN 480 ELSE CALL MOTION(#1,-5,-10)
490 CALL SOUND(-999,-3,8) :: CALL POSITION(#1,Y,X) :: IF Y>18 THEN 490 ELSE CALL MOTION(#1,0,-10)
500 FOR A=8 TO 30 :: CALL SOUND(-999,-3,A) :: NEXT A
510 HA=INT(RND*50+95) :: CALL SPRITE(#10,100,2,153,HA)
520 FOR A=0 TO 9 :: WI(A)=INT(RND*14-7) :: IF WI(A)=0 THEN WI(A)=1
530 NEXT A :: GOSUB 1910 :: BAL,PE,LA,LV,SC=0 :: SS=15
540 BAL=BAL+1 :: BCO=BAL*3
550 L=30 :: CALL SPRITE(#2,96,BCO,150,240,0,-5,#13,104,16,152,237,0,-5)
560 CALL POSITION(#2,Y,X) :: IF X>HA+4 THEN CALL SOUND(1,-3,L) :: L=40-L :: GOTO 560 ELSE CALL MOTION(#2,0,0,#13,0,-7)
570 CALL POSITION(#13,Y,X) :: IF X>HA-18 THEN CALL SOUND(1,-3,L) :: L=40-L :: GOTO 570 ELSE CALL MOTION(#13,0,0)
580 SND$=RPT$("^",SS) :: COL=1 :: CO2=BCO+1 :: DISPLAY AT(24,1):"HOCH ZUM START" :: CALL TALK(VSTART$)
590 CALL KEY(0,K,S) :: CALL JOYST(1,X,Y) :: FOR D=1 TO 40 :: NEXT D :: IF Y<4 AND K<>ASC(UP$)THEN CALL COLOR(#2,COL) :: COL=CO2-COL :: GOTO 590
600 CALL COLOR(#2,BCO) :: YD=-1.5 :: DISPLAY AT(24,1):SND$;TAB(18);"ALT.: 0@" :: ALM=0
610 IF RND>.02 THEN 630 ELSE H=INT(RND*10) :: WI(H)=INT(RND*14-7)*1.1^(LAND/2) :: WI(H)=MAX(-16,MIN(16,WI(H)))
620 DISPLAY AT(24,1)SIZE(17):"WIND DREHT (";STR$((9-H)*80);"@)" :: GOSUB 1910
630 CALL POSITION(#2,Y,X) :: AL=(150-Y)*5 :: DISPLAY AT(24,24):STR$(MAX(0,AL));"@" :: ALM=MAX(AL,ALM)
640 CALL MOTION(#2,YD,WI(INT(Y/16))) :: CALL POSITION(#2,Y,X) :: IF Y<16 THEN CALL MOTION(#2,0,0) :: YD=0
650 IF Y<151 THEN 670
660 CALL COINC(#2,#10,18,C) :: IF C=0 THEN CALL SPRITE(#11,140,BCO,Y,X) :: GOSUB 1940 :: FU=1 :: GOTO 850 ELSE 1070
670 CALL COINC(#1,#2,16,CO) :: IF X<30 OR X>218 OR CO THEN CALL SPRITE(#11,140,BCO,Y,X) :: GOSUB 1940 :: GOTO 830
680 IF JT$="J" THEN 720
690 CALL KEY(0,K,S) :: IF S=0 THEN 610 ELSE IF K=ASC(AS$)THEN 770
700 IF K=ASC(DW$)THEN YY=-4 :: GOTO 740
710 IF K=ASC(UP$)THEN 750 ELSE 610
720 CALL KEY(1,K,S) :: IF S<>0 THEN 770
730 CALL JOYST(1,XX,YY) :: IF YY=0 THEN 610
740 IF YY=-4 THEN YD=MIN(YD+1.5,6) :: CALL SOUND(300,-5,8) :: GOTO 610
750 IF SND$=RPT$(" ",SS)THEN 610 ELSE SND$=SEG$(SND$&" ",2,SS) :: DISPLAY AT(24,1)SIZE(17):SND$ :: CALL SPRITE(#11,32,2,Y,X,25,0)
760 CALL POSITION(#11,Y,X) :: IF Y<144 THEN 760 ELSE CALL DELSPRITE(#11) :: CALL SOUND(1,-7,6) :: YD=MAX(YD-1.5,-6) :: GOTO 610
770 CALL SPRITE(#11,140,BCO,Y,X,5,-2-(X<128)*4) :: DISPLAY AT(24,1):"** AUSGESTIEGEN **" :: FU,BD=0 :: YD=-6 :: SC=SC-100
780 IF BD=1 THEN 810
790 CALL POSITION(#2,Y,X) :: CALL MOTION(#2,YD,WI(INT(Y/16))) :: IF Y<10 THEN CALL MOTION(#2,0,0) :: YD=0
800 CALL COINC(#1,#2,16,CO) :: IF X<30 OR X>218 OR CO THEN GOSUB 1940 :: BD=1
810 CALL POSITION(#11,Y,X) :: IF Y>144 THEN CALL MOTION(#11,0,0) :: FU=1
820 IF FU=0 OR BD=0 THEN 780 ELSE 850
830 XD=-2-(X<128)*4 :: CALL MOTION(#11,5,XD)
840 CALL POSITION(#11,Y,X) :: IF Y<145 THEN 840 ELSE CALL MOTION(#11,0,0) :: GOTO 850
850 CALL TALK(VEND$) :: CALL SPRITE(#12,36,16,145,240,0,-10) :: N=300
860 CALL POSITION(#12,YY,XX) :: IF XX>X THEN CALL SOUND(-500,N,7,N+100,10) :: N=700-N :: GOTO 860 ELSE CALL MOTION(#12,0,0)
870 FOR A=1 TO 8 :: CALL SOUND(-1,INT(RND*3-7),INT(RND*15+5)) :: NEXT A
880 CALL DELSPRITE(#11) :: CALL MOTION(#12,0,-14) :: N=300
890 CALL POSITION(#12,YY,XX) :: IF XX>8 THEN CALL SOUND(-500,N,7,N+100,10) :: N=700-N :: GOTO 890 ELSE CALL DELSPRITE(#12)
900 IF BAL<3 AND FU=1 THEN L=30 :: CALL MOTION(#13,0,7) :: FU=0 ELSE 930
910 CALL POSITION(#13,Y,X) :: IF X<235 THEN CALL SOUND(1,-3,L) :: L=40-L :: GOTO 910 ELSE CALL MOTION(#13,0,0)
920 SC=SC-100 :: GOTO 540
930 FOR A=120 TO 660 STEP 20 :: CALL SOUND(-100,A,6) :: NEXT A
940 DISPLAY AT(6,7)SIZE(15):")) GAME OVER (("
950 IF FU=0 THEN DISPLAY AT(8,3)SIZE(24):"DU LIEGST IM KRANKENHAUS" :: SC=SC-200 ELSE DISPLAY AT(8,3)SIZE(21):"KEINE BALLONS MEHR DA"
960 GL$="GUTE LANDUNGEN:"&STR$(LA) :: VP$="UND VERPATZT  :"&STR$(PE) :: DISPLAY AT(11,5)SIZE(LEN(GL$)):GL$ :: DISPLAY AT(13,5)SIZE(LEN(VP$)):VP$
970 SC$=STR$(SC)&" PUNKTE" :: DISPLAY AT(16,14-LEN(SC$)/2)SIZE(LEN(SC$)):SC$
980 DISPLAY AT(18,7)SIZE(16):"NOCHMAL (J/N)?"
990 CALL KEY(3,K,S) :: IF K<>74 AND K<>78 THEN 990 ELSE DISPLAY AT(18,22)SIZE(1):CHR$(K)
1000 FOR A=6 TO 18 :: CALL HCHAR(A,4,120,25) :: NEXT A
1010 IF K=74 THEN 510
1020 DISPLAY AT(12,3)SIZE(23):"ADIOS,TSCHUESS,GOOD BYE"
1030 GOSUB 1860
1040 FOR D=1 TO 1000 :: NEXT D
1050 CALL DELSPRITE(ALL) :: CALL CLEAR
1060 END
1070 CALL MOTION(#2,0,0) :: LA=LA+1 :: LV=1-LV :: IF LV=1 AND SS>5 THEN SS=SS-1 :: DISPLAY AT(24,1):"EIN SANDSACK WENIGER." :: GOTO 1100
1080 DISPLAY AT(24,1):"TOWER:MEHR WIND!" :: FOR A=0 TO 9 :: IF WI(A)>-16 AND WI(A)<16 THEN WI(A)=WI(A)*1.1
1090 NEXT A
1100 CALL TALK(VDANG$) :: SC=SC+ALM*5+100*LA :: CALL LOCATE(#2,150,HA+4)
1110 CALL POSITION(#13,Y,X) :: FOR A=1 TO 15 :: CALL SOUND(1,-5,5) :: CALL LOCATE(#13,Y-5,X) :: FOR D=1 TO 70 :: NEXT D :: CALL LOCATE(#13,Y,X)
1120 NEXT A :: IF LA/3<>INT(LA/3)THEN 580 ELSE DISPLAY AT(24,1):"DEINE ";STR$(LA);". LANDUNG HIER!" :: SC=SC+200
1130 CALL TALK(VGOOD$)
1140 CALL POSITION(#1,Y,X) :: IF X>146 OR X<110 THEN 1140
1150 FOR A=14 TO 16 :: CALL POSITION(#1,Y,X) :: CALL SOUND(1,110,5,-5,4) :: CALL SPRITE(#A,140,3+(A-14)*2,Y,X,5,0) :: FOR D=1 TO 20 :: NEXT D :: NEXT A
1160 IF BAL>1 THEN DISPLAY AT(24,1):"EIN ZUSATZBALLON!" :: BAL=BAL-1
1170 FOR A=1 TO 3
1180 CALL POSITION(#A+13,Y,X) :: IF Y<150 THEN 1180 ELSE CALL MOTION(#A+13,0,0)
1190 CALL SOUND(1,-6,2) :: NEXT A :: CALL DELSPRITE(#14,#15,#16) :: CALL MOTION(#13,0,7)
1200 CALL POSITION(#13,Y,X) :: IF X<HA-4 THEN CALL SOUND(1,-3,L) :: L=40-L :: GOTO 1200 ELSE CALL MOTION(#13,0,5,#2,0,5)
1210 CALL POSITION(#2,Y,X) :: IF X<240 THEN CALL SOUND(1,-3,L) :: L=40-L :: GOTO 1210 ELSE CALL MOTION(#13,0,0) :: CALL DELSPRITE(#2)
1220 BAL=BAL-1 :: GOTO 540
1230 CALL HCHAR(1,1,120,768) :: CALL HCHAR(24,1,92,32)
1240 CALL CHAR(32,"FF000000000000FF")
1250 FOR A=1 TO 8 :: CALL VCHAR(A,3,111+A) :: CALL VCHAR(A,31,120-A) :: NEXT A :: FOR A=9 TO 16 :: CALL VCHAR(A,3,128-A) :: CALL VCHAR(A,31,103+A) :: NEXT A
1260 FOR A=17 TO 23 :: CALL VCHAR(A,3,95+A) :: CALL VCHAR(A,31,136-A) :: NEXT A
1270 CALL VCHAR(24,3,91) :: CALL VCHAR(24,31,93)
1280 CALL SPRITE(#16,128,16,3,100,0,4,#17,128,16,8,30,0,5,#18,128,16,22,190,0,6,#19,128,16,3,2,0,7)
1290 DISPLAY AT(6,8)SIZE(12):"|~\x5c<FLUGTAG>" :: DISPLAY AT(7,8)SIZE(2):"}";CHR$(127)
1300 DISPLAY AT(9,6)SIZE(18):"|~\x5c<AUF UND DAVON>" :: DISPLAY AT(10,6)SIZE(2):"}";CHR$(127)
1310 DISPLAY AT(12,6)SIZE(18):"|~\x5c<IM LUFTBALLON>" :: DISPLAY AT(13,6)SIZE(2):"}";CHR$(127)
1320 DISPLAY AT(19,5)SIZE(20):"|~\x5c<ZUM START TASTE>" :: DISPLAY AT(20,5)SIZE(2):"}";CHR$(127)
1330 CALL SPRITE(#4,100,3,177,103,#5,100,6,177,120,#6,100,9,177,137)
1340 CALL SPRITE(#1,96,3,175,107,0,0,#2,96,6,175,124,0,0,#3,96,9,175,141,0,0) :: FOR D=1 TO 50
1350 CALL KEY(0,K,S) :: IF S=1 THEN 1490
1360 NEXT D
1370 FOR A=1 TO 3 :: CALL MOTION(#A,-2,0) :: CALL SOUND(10,-7,3) :: FOR D=1 TO 30+A*20 :: NEXT D :: NEXT A
1380 CALL MOTION(#INT(RND*3+1),-4,INT(RND*10-5))
1390 FOR A=1 TO 3 :: CALL POSITION(#A,Y,X) :: IF Y<20 THEN CALL MOTION(#A,0,0) :: CALL COLOR(#A,7) :: CALL PATTERN(#A,136) :: PENG=PENG+1 :: CALL SOUND(200,-7,3)
1400 IF X<30 THEN CALL MOTION(#A,-4,5) ELSE IF X>215 THEN CALL MOTION(#A,-4,-5)
1410 CALL KEY(0,K,S) :: IF S=1 THEN 1490
1420 IF PENG=3 THEN 1440
1430 NEXT A :: GOTO 1380
1440 FOR A=1 TO 3 :: CALL PATTERN(#A,140) :: CALL COLOR(#A,A*3) :: CALL MOTION(#A,5,0) :: NEXT A
1450 CALL MOTION(#INT(RND*3+1),5,INT(RND*10-5)) :: CALL KEY(0,K,S) :: IF S=1 THEN 1490
1460 FOR A=1 TO 3 :: CALL POSITION(#A,Y,X) :: IF Y>165 THEN CALL MOTION(#A,0,0) :: CALL SOUND(10,-3,5) :: CALL DELSPRITE(#A) :: PENG=PENG-1 :: IF PENG=0 THEN 1340
1470 IF X<30 THEN CALL MOTION(#A,4,5) ELSE IF X>215 THEN CALL MOTION(#A,4,-5)
1480 NEXT A :: CALL KEY(0,K,S) :: IF S<1 THEN 1450
1490 CALL DELSPRITE(ALL) :: CALL CHAR(32,"") :: CALL CLEAR
1500 FOR A=65 TO 90 :: CALL CHARPAT(A,A$) :: CALL CHAR(A,"00"&SEG$(A$,3,12)&"00") :: NEXT A
1510 DISPLAY AT(6,11):"FLUGTAG" :: DISPLAY AT(7,11):"-------" :: DISPLAY AT(9,1):"(C) BY MG-SOFTWARE 1984"
1520 DISPLAY AT(11,1):"GEWIDMET DEM LSC LEVERKUSEN"
1530 DISPLAY AT(14,1):"SPEECH-SYNTHESIZER (J/N)? J" :: DISPLAY AT(16,1):"ANLEITUNG NOETIG (J/N)? N"
1540 DISPLAY AT(23,2):"BITTE ALPHA LOCK EINRASTEN"
1550 ACCEPT AT(14,27)VALIDATE("JN")SIZE(-1):SP$ :: IF SP$="" THEN 1550
1560 ACCEPT AT(16,25)VALIDATE("JN")SIZE(-1):IN$ :: IF IN$="" THEN 1560
1570 IF SP$="N" THEN 1590
1580 VDANG$="LOOK OUT" :: VSTART$="PRESS UP TO START" :: VEND$="I AM SORRY BUT YOU WERE NOT GOOD" :: VGOOD$="YOU WERE VERY GOOD"
1590 CALL CLEAR :: IF IN$="J" THEN GOSUB 1720
1600 DISPLAY AT(11,1):"HOCH.....--)" :: DISPLAY AT(13,1):"RUNTER...--)" :: DISPLAY AT(15,1):"ABSPRUNG --)"
1610 DISPLAY AT(6,1):"(J)OYSTICK / (T)ASTATUR? -)T" :: ACCEPT AT(6,28)SIZE(-1)VALIDATE("JT"):JT$ :: IF JT$="" THEN 1610
1620 IF JT$="J" THEN DISPLAY AT(8,2):"BITTE ALPHA LOCK LOESEN" ELSE DISPLAY AT(8,2):"BITTE ALPHA LOCK EINRASTEN"
1630 IF JT$="T" THEN 1660
1640 UP$=" "
1650 DISPLAY AT(11,13):"J1 HOCH" :: DISPLAY AT(13,13):"J1 RUNTER" :: DISPLAY AT(15,13):"FIRE (J1)" :: GOTO 1700
1660 FOR A=11 TO 15 STEP 2 :: DISPLAY AT(A,13) :: NEXT A
1670 VA$="=/,. " :: ACCEPT AT(11,13)SIZE(1)VALIDATE(UALPHA,NUMERIC,VA$):UP$ :: IF UP$="" THEN UP$=" "
1680 ACCEPT AT(13,13)SIZE(1)VALIDATE(UALPHA,NUMERIC,VA$):DW$ :: IF DW$="" THEN DW$=" "
1690 ACCEPT AT(15,13)SIZE(1)VALIDATE(UALPHA,NUMERIC,VA$):AS$ :: IF AS$="" THEN AS$=" "
1700 DISPLAY AT(20,1):"(S)PIEL/(A)ENDERN? S" :: ACCEPT AT(20,20)VALIDATE("SA")SIZE(-1):A$ :: IF A$="A" THEN 1610
1710 CALL CLEAR :: RETURN
1720 PRINT TAB(11);"FLUGTAG": :
1730 PRINT "ZIEL VON *FLUGTAG* IST ES,  MIT EINEM BALLON MOEGLICHST OFT HOCH AUFZUSTEIGEN UND   WIEDER AM GLEICHEN ORT ZU"
1740 PRINT "LANDEN.  GESTEUERT WIRD DER BALLON DURCH GASABLASSEN    (SINKEN) UND BALLASTABWURF  (STEIGEN).DER BALLON WIRD"
1750 PRINT "DURCH WINDE HIN- UND HER-   GERISSEN. STOESST MAN GEGEN DAS FLUGZEUG ODER DIE SEILE,SO IST SPIELENDE,AUSSER,"
1760 PRINT "MAN HATSICH DURCH EINEN AB- SPRUNG GERETTET."
1770 PRINT : :TAB(25);")))"
1840 CALL KEY(0,K,S) :: IF S<1 THEN 1840 ELSE CALL CLEAR :: RETURN
1850 GOTO 1850
1860 CALL POSITION(#1,Y,X) :: IF X<200 OR X>220 THEN 1860 ELSE CALL MOTION(#1,11,-10)
1870 CALL SOUND(-999,-3,14) :: CALL POSITION(#1,Y,X) :: IF Y<166 THEN 1870 ELSE CALL MOTION(#1,0,-8)
1880 CALL SOUND(-999,-3,20) :: CALL POSITION(#1,Y,X) :: IF X>20 THEN 1880 ELSE CALL MOTION(#1,0,0)
1890 FOR A=20 TO 30 :: CALL SOUND(-999,-3,A) :: NEXT A
1900 RETURN
1910 IF WI(9)<1 THEN CALL CHAR(100,FLGL$) ELSE CALL CHAR(100,FLGR$)
1920 FOR W=16 TO 19 :: CALL MOTION(#W,0,(W-12)*SGN(WI(0))) :: NEXT W
1930 RETURN
1940 CALL SOUND(500,-6,3) :: CALL COLOR(#2,7) :: CALL MOTION(#2,0,0) :: CALL PATTERN(#2,136)
1950 FOR D=1 TO 200 :: NEXT D :: CALL DELSPRITE(#2) :: DISPLAY AT(24,1):"  DEIN BALLON IST GEPLATZT."
1960 PE=PE+1 :: RETURN
1970 SUB TALK(S$)
1980 IF S$="" THEN SUBEXIT ELSE CALL SAY(S$&",,OVER")
1990 SUBEND